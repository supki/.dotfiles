function chpwd {
	print -Pn "\e]2;%~\a"
}

autoload -U compinit promptinit colors
autoload -Uz vcs_info

autoload -U zrecompile
zrecompile -pq ~/.zshrc
zrecompile -pq ~/.zshenv

fpath=(/opt/devtools-1.0/completions/zsh ~/git/zsh-completions/src ~/git/dotfiles/completion $fpath)
compinit
setopt autocd autopushd correct
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache
zstyle ':completion:*:descriptions' format '%U%B%d%b%u'
zstyle ':completion:*:warnings' format '%BSorry, no matches for: %d%b'

setopt prompt_subst
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '×'
zstyle ':vcs_info:*' unstagedstr '×'
zstyle ':vcs_info:svn:*' formats "%F{3}%b%f"
zstyle ':vcs_info:git:*' formats "%F{1}%u%f%F{2}%c%f %F{3}%b%f"
zstyle ':vcs_info:*' enable git svn

function vcs_info_wrapper {
  vcs_info
  if [ -n "$vcs_info_msg_0_" ]; then
    echo "%{$fg[grey]%}${vcs_info_msg_0_}%{$reset_color%}$del"
  fi
}

function nix_env_wrapper {
  if [ -n "${IN_NIX_SHELL:-}" ]; then
    echo -n "[$name]"
  fi
}

function go_env_wrapper {
  if [ -n "${GOPATH:-}" ]; then
    echo -n "[$GOPATH]"
  fi
}

promptinit
colors
PROMPT="%M:%F{yellow}%~$prompt_newline%(?,%F{green},%F{red})%#%f "
PROMPT2="%F{blue}%_%f> "
RPROMPT="%{"$'\e[1A'"%}"$'$(vcs_info_wrapper)'' ''$(nix_env_wrapper)$(go_env_wrapper)'"%{"$'\e[1B'"%"

chpwd

HISTSIZE=10000
HISTFILE=~/.zhistory
SAVEHIST=10000

setopt histignorealldups
setopt histignorespace

setopt autocd

setopt interactivecomments
setopt nonomatch

bindkey '^P' history-beginning-search-backward
bindkey '^N' history-beginning-search-forward
bindkey '^[[A' history-beginning-search-backward
bindkey '^[[B' history-beginning-search-forward
bindkey -a 'k' history-beginning-search-backward
bindkey -a 'j' history-beginning-search-forward
bindkey '^?' backward-delete-char
bindkey '^[[3~' delete-char
bindkey "^[[7~" beginning-of-line
bindkey "^[[8~" end-of-line
bindkey "^[[1~" beginning-of-line
bindkey "^[[4~" end-of-line
bindkey "^_" undo
bindkey -a "q" push-line
bindkey "^[[Z" reverse-menu-complete
bindkey '\e#' pound-insert

alias ls='ls --color -1'
alias less='less -R'
alias ka='killall'
alias c='cabal'
alias g='git'
alias s='svn'
alias e='editor'
alias v='vagrant'
alias t='tmux'
alias k='knife'
alias d='docker'
alias x='container'
alias l='less'
alias hr='echo -e "\e[1;38;05;224m${(l:COLUMNS::❤:)}\e[0m"'
alias diff='git diff --no-index'
alias update-package.nix="nix-shell -p 'cabal2nix' --run 'cabal2nix . > package.nix'"

function nix-zsh() {
	nix-shell --command "$SHELL" "$@"
}

function man {
	/usr/bin/man $@ | ifne vim -X -c 'set ft=man nomod' -c 'nmap q :qa!<CR>' -
}

function haddock-dist {
	for index in dist/doc/html/*/index.html; do
		x-www-browser file://$(pwd)/${index}
	done
}

function j\* {
	declare xs
	xs=("${jobtexts[@]}")

	case ${#xs} in
		0) kill -SIGINT $$ ;; # totally not a hack
		1) fg "${xs[@]}" ;;
		*) fg "$(for x in "${xs[@]}"; do echo "$x"; done | wybor --prompt 'job> ')" ;;
	esac
}

zle -N back-to-vim 'j\*'
bindkey -a "^z" back-to-vim

ssh-add 2>/dev/null # add my identity to ssh-agent and don't tell me about this, thanks

source ~/git/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

export NO_SCRIPTS_NOTIFICATIONS=yes

export PYTHONSTARTUP=${HOME}/.pythonrc

export CHO_PREFIX=${HOME}/ghc

export GOROOT=${HOME}/go

export TERM=screen-256color

function mkcdir {
	mkdir --parents "$1" && cd "$_";
}

function fuck-them {
	for pat in "$@"; do
		pgrep "${pat}" | xargs kill -9
	done
}

function sd {
	svn diff "$@" | colordiff | diff-highlight | less --quit-if-one-screen --no-init --RAW-CONTROL-CHARS
}

function svn {
	case $1 in
		st|status|up|update|sw|switch)
			command svn --ignore-externals "$@" && \
				if type externals >/dev/null; then
					externals up
				fi
			;;

		*)
			command svn "$@"
			;;
		esac
}

if [ -n "$NIX_GHC" ]; then
	NIX_GHCI=${NIX_GHC}i
fi
